group 'org.cen.tigerisland'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

configurations {
  cucumberRuntime {
    extendsFrom testRuntime
  }
}

task cucumber() {
  dependsOn assemble, compileTestJava
  doLast {
    javaexec {
      main = "cucumber.api.cli.Main"
      classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', 'pretty', '--glue', 'src/test/resources']
    }
  }
}

dependencies {
  testCompile 'info.cukes:cucumber-java:1.2.5'
  testCompile 'info.cukes:cucumber-junit:1.2.5'
  testCompile 'junit:junit:4.11'
  compile group: 'info.cukes', name: 'cucumber-core', version: '1.1.5'
}

sourceSets {
  main {
    java {
      srcDirs = ["src/main"]
    }
  }
  test {
    java {
      srcDirs = ["src/test"]
    }
  }
}
repositories {
    mavenCentral()
    jcenter()
}

jacoco {
  toolVersion = '0.7.1.201405082137'
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
  dependsOn = subprojects.test
  sourceDirectories = files(sourceSets.main.allSource.srcDirs)
  classDirectories =  files(sourceSets.main.output)
  executionData = files(subprojects.jacocoTestReport.executionData)
  reports {
    html.enabled = true
    xml.enabled = true
    csv.enabled = false
  }
}
